plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    def final appName = "ViuIt"

    compileSdkVersion 30

    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        versionProps['VERSION_CODE'] = '0'
    }

    def code = versionProps['VERSION_CODE'].toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        applicationId "id.krisna.viuit"
        minSdk 21
        targetSdkVersion 30
        versionCode code
        versionName "1.0.0(" + code + ")"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        production {
            keyAlias 'viuit'
            keyPassword 'viuit2022'
            storeFile file("$rootDir/production_keystore.jks")
            storePassword 'viuit2022'
        }

        staging {
            keyAlias 'viuit'
            keyPassword 'viuit2022'
            storeFile file("$rootDir/staging_keystore.jks")
            storePassword 'viuit2022'
        }
    }

    buildTypes {
        release {
            ext.enableCrashlytics = true
            debuggable false
            versionNameSuffix '-RELEASE'
        }
        debug {
            versionNameSuffix '-DEBUG'
        }
    }

    flavorDimensions "version"

    productFlavors {
        staging {
            signingConfig signingConfigs.staging
            applicationIdSuffix '.staging'
            dimension "version"
            buildConfigField "String", 'BASE_URL', '"https://devel.bebasbayar.com/"'
            resValue "string", "app_name", appName + " - staging"

        }
        production {
            signingConfig signingConfigs.production
            dimension "version"
            buildConfigField "String", 'BASE_URL', '"https://devel.bebasbayar.com/"'
            resValue "string", "app_name", appName

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {

    /*ROOT*/
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'

    /*UI*/
    def appcompat_version = "1.3.1"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    def legacy_support_version = "1.0.0"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"

    def material_drawer_nav_version = "7.0.0-rc07"
    implementation "com.mikepenz:materialdrawer-nav:$material_drawer_nav_version"

    def material_version = "1.4.0"
    implementation "com.google.android.material:material:$material_version"

    def constraint_layout_version = "2.1.0"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    def activity_ktx_version = "1.3.1"
    implementation "androidx.activity:activity-ktx:$activity_ktx_version"

    def fragment_ktx_version = "1.3.6"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx_version"

    implementation('com.alimuzaffar.lib:pinentryedittext:2.0.6') { exclude group: 'androidx.appcompat', module: 'appcompat' }

    def pager_indicator_version = "2.4.1"
    implementation "com.github.JakeWharton:ViewPagerIndicator:$pager_indicator_version"

    implementation 'com.google.android.gms:play-services-auth:20.1.0'

    implementation 'de.hdodenhof:circleimageview:3.1.0'

    implementation 'com.github.d-max:spots-dialog:1.1@aar'

    implementation 'com.github.takusemba:spotlight:2.0.4'

    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:8.0.1'

    implementation 'com.github.mreram:showcaseview:1.4.0'

    implementation 'com.github.Jay-Goo:RangeSeekBar:3.0.0'

    /*Rx Java*/
    def rxandroid_version = "2.1.1"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    /*Network*/
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    def okhttp_version = "4.9.1"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    /*Utility*/
    def eventbus_version = "3.1.1"
    implementation "org.greenrobot:eventbus:$eventbus_version"
    implementation 'id.zelory:compressor:2.1.0'

    def timber_version = "4.7.1"
    implementation "com.jakewharton.timber:timber:$timber_version"

    def preference_ktx_version = "1.1.1"
    implementation "androidx.preference:preference-ktx:$preference_ktx_version"

    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    /*Dependency Injection*/
    def hilt_version = "2.38"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    def hilt_lifecycle = "1.0.0-alpha03"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_lifecycle"
    // When using Kotlin.
    kapt "androidx.hilt:hilt-compiler:$hilt_lifecycle"


    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'junit:junit:4.13.2'

    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

//    implementation 'androidx.appcompat:appcompat:1.4.2'
//    implementation 'com.google.android.material:material:1.6.0'
//    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
//    testImplementation 'junit:junit:4.+'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}